function fish_prompt --description 'Write out the prompt'
    set -l last_status $status
    set -l normal (set_color normal)
    set -l status_color (set_color brgreen)
    set -l cwd_color (set_color $fish_color_cwd)
    set -l vcs_color (set_color brpurple)
    set -l prompt_status ""

    # –î–ª–∏–Ω–∞ –ø—É—Ç–∏ (0 = –ø–æ–ª–Ω—ã–π –ø—É—Ç—å)
    set -q fish_prompt_pwd_dir_length
    or set -lx fish_prompt_pwd_dir_length 0

    # –û–ø—Ä–µ–¥–µ–ª—è–µ–º —Å—É—Ñ—Ñ–∏–∫—Å (–∏–∫–æ–Ω–∫—É)
    set -l suffix 'üêü'
    if functions -q fish_is_root_user; and fish_is_root_user
        set suffix '#'
        set cwd_color (set_color $fish_color_cwd_root)
    else if set -q __sudo_using_rs; and test "$__sudo_using_rs" = "1"
        set suffix 'ü¶Äüêü'
    end

    # –ï—Å–ª–∏ –±—ã–ª–∞ –æ—à–∏–±–∫–∞ - –∫—Ä–∞—Å–∏–º –≤ –∫—Ä–∞—Å–Ω—ã–π –∏ –≤—ã–≤–æ–¥–∏–º –∫–æ–¥
    if test $last_status -ne 0
        set status_color (set_color $fish_color_error)
        set prompt_status $status_color "[" $last_status "]" $normal
    end

    # –°–±–æ—Ä–∫–∞ –ø—Ä–æ–º–ø—Ç–∞: [user@host] [path] [git] [status]
    # –ù–∞ –Ω–æ–≤–æ–π —Å—Ç—Ä–æ–∫–µ –≤—ã–≤–æ–¥–∏–º –∏–∫–æ–Ω–∫—É
    echo -s (prompt_login) ' ' $cwd_color (prompt_pwd) $vcs_color (fish_vcs_prompt) $normal ' ' $prompt_status
    echo -n -s $status_color $suffix ' ' $normal
end
